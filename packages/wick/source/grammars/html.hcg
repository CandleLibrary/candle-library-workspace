# 
# Wick HTML Parser file
# 

@IGNORE g:sp g:nl 
    
<> HTML > ! RESULT

<> RESULT > DTD(*) BODY                                                                     
                                                                f:r { $2.DTD = $1, $2 }

<> BODY > GOAL_TAG

<> GOAL_TAG > ! TAG

<> TAG > 
        tk:TAG_HEAD ATTRIBUTES? \> TAG_BODY?  \</ (RST g:sp g:nl ) TAGNAME (RST g:sp g:nl ) \>                                     

        f:r { { type:env.typ["HTML_"+$1.slice(1).toUpperCase()] || env.typ.HTML_Element, tag:$1.slice(1).toUpperCase(), attributes:$$2 || [], nodes:$4 || [], VOID:false, pos } }

    |   tk:TAG_HEAD ATTRIBUTES? \/>                                                             

        f:r { { type:env.typ["HTML_"+$1.slice(1).toUpperCase()] || env.typ.HTML_Element, tag:$1.slice(1).toUpperCase(), attributes:$$2 || [], nodes:[], VOID:false, pos } }

    |   VOID_TAG_HEAD ATTRIBUTES? \/ ? >                                                          

        f:r { { type:env.typ["HTML_"+$1.slice(1).toUpperCase()] || env.typ.HTML_Element, tag:$1.slice(1).toUpperCase(), attributes:$$2 || [], nodes:[], VOID:true, pos } }
    
<> DTD > 

    \<! tk:identifier ? \--  ( g:id | g:sym | g:sp | g:nl | g:num  )(*\" )  \-- \>                                                 
    
    f:r { { type:"DTD", tag:$$2|| "", value:$$4 }  }                


###########################################################################################
# https://www.w3.org/TR/2011/WD-html-markup-20110113/syntax.html#syntax-elements
###########################################################################################

<> TAG_HEAD > \< TAGNAME


<> VOID_TAG_HEAD > \<input
    | \<area
    | \<base
    | \<br
    | \<col
    | \<command
    | \<embed
    | \<hr
    | \<img
    | \<keygen
    | \<link
    | \<meta
    | \<param
    | \<source
    | \<track
    | \<wbr

<> TAG_BODY > TAG_BODY_ITEM(+)

<> TAG_BODY_ITEM > TEXT_NODE                        
    | TAG
    | DTD                                                                                                                            
    
<> ATTRIBUTES > ATTRIBUTE(+)                                    

<> ATTRIBUTE > ATTRIBUTE_HEAD ( \= ATTRIBUTE_BODY )?
                                                                f:r { {type:env.typ.HTMLAttribute, name:$1, value:$2 || "", pos } }

<> ATTRIBUTE_HEAD > \' (RST g:sp g:nl ) tk:identifier \'                                                             
                                                                f:r { $2 }
    | \" (RST g:sp g:nl ) tk:identifier \"                                                                           
                                                                f:r { $2 }
    | tk:identifier

<> ATTRIBUTE_BODY >                                                       
    tk:ATTRIBUTE_STRING_VALUE                                                                   
                                                                f:r { $1.slice(1,-1) }
    | tk:identifier

    | g:num          

<> ATTRIBUTE_STRING_VALUE >  \" BODY_SYMBOLS? \"
    | \' BODY_SYMBOLS? \'

<> TAGNAME > tk:identifier

<> identifier > g:id 
    | identifier (RST g:sp) g:id 
    | identifier (RST g:sp) \- 
    | identifier (RST g:sp) \: 
    | identifier (RST g:sp) g:num 

<> TEXT_NODE > TEXT_NODE_DATA                                                                   
                                                                f:r { {type:env.typ.HTMLText, data:$1, pos } }

<> TEXT_NODE_DATA > ( g:id | g:sym | g:sp | g:nl | g:num  )(*\" )

<> BODY_SYMBOLS > BODY_SYMBOL
    | BODY_SYMBOLS BODY_SYMBOL
 
<> BODY_SYMBOL > g:id 
    | g:nl 
    | g:sp 
    | g:sym 
    | g:num 
    | \! 
    | \( 





